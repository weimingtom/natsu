*enter
//保存进来时的字体，一边切出去的时候还原
@storefont

//改变字体
@textstyle name="font/NotoSansCJKsc-Regular" size=24 color=#87cefa shadow=false stroke=false bold=false italic=false strike=false

//禁用事件
@disable_dialogbox

//设定每页行数（大概放在sf里会更好？）
#tf.line_per_page=15;

//创建底层
@layer index=1000 opacity=0 width=1280 height=720

//创建文字层
@layer index=1004 opacity=0 width=1280 height=720

//读取背景并添加到底层上。
@sprite index=1001 file="bg"
@addto index=1001 target=1000 opacity=204 zorder=10
//将底层已透明的形式添加到基础层上
@addto index=1000 target=basic_layer opacity=0 zorder=51
//将文字层添加到底层上
@addto index=1004 target=1000 opacity=255 zorder=11

//创建按钮，并设定好锚点
@button index=1002 idle="system/backbutton"  hover="system/backbutton_on" click="system/backbutton_on"  onclick="exits" threshold=20
@anchor index=1002 set="topright"
//创建滑条，因为滑块的前端在滑条上所以用层来进行位置上的调整（虽然感应区域还是只有创建的层的大小，待改进）
@layer index=1005 width=120 height=44
@sprite index=1006 file="system/history/block"
@addto index=1006 target=1005 pos=[24,0]
@sprite index=1007 file="system/history/slider"
@sliderex index=1003 block=1005 back=1007 min=tf.line_per_page value=History.count max=History.count vertical=true onchanged='hsbar'
//添加按钮和滑条
@addto index=1002 target=1001 zorder=50 pos=[719,658] opacity=255 
@addto index=1003 target=1001 zorder=50 pos=[1191,70] opacity=255 

//将底层渐入显示出来
@action mode="fadeto" target=1000 opacity=255 time=300
@waitaction index=1000

//重设mousewheel等事件
@deleteevent name="mousewheel"
@event name="mousewheel" type="callback" enable=true label="*history_scroll"// ignore=true
@event name="hsbar" type="callback" enable=true label="*show_history_bar"// ignore=true
@event name="exits" type="jump" enable=true label="*history_exit"
@event name="rclick" type="jump" enable=true label="*history_exit"
#tf.hisStart=0;

//第一次的历史要手动调用显示
@call "*show_history_bar"
@waitbutton
*show_history_bar
//@event name="hsbar" enable=false
//@spriteopt index=1003 disable=true
#tf.hisStart=History.count-Sprite(1003).value;

*show_history
//显示历史界面，总共显示条数15，占用精灵编号1005-1019
//@event name="mousewheel" enable=false

##
if(tf.hisStart>History.count-tf.line_per_page+1){tf.hisStart=History.count-tf.line_per_page+1;}
if(tf.hisStart<0){tf.hisStart=0;}

tf.hisEnd=tf.hisStart+tf.line_per_page-1;
if(tf.hisEnd>History.count-1){ tf.hisEnd=History.count-1;}
var index=1005;
var y=74;
var next;
var text;

var maxLength;
##
@pretrans
@removeall index=1004 delete=true

@for var=i range=range(tf.hisEnd,tf.hisStart-1)
	
	##
	text=History.getDataAtLine(i).text;
	next=5;
	##
	@textsprite index=index text=text size=25 color=#87cefa xinterval=2
	@addto index=index target=1004 pos=[116,y]
	##
	next+=Sprite(index).height;
	y+=next;
	index++;
	##
@next
@trans mode="crossfade" index=basic_layer time=0
@waittrans index=1000
//@event name="mousewheel" enable=true
//@event name="hsbar" enable=true
//@spriteopt index=1003 disable=false
/*
@screenshot index=10 target=basic_layer
@savetoimage index=10 file="screenshot/test2"
*/
@return

	
*history_scroll
@if "System.mouseWheel>0"
	#Sprite(1003).value-=1
	//不需要额外jump，Sprite的value变动时会自动调用onchange事件
//	@jump label="*show_history_bar"
@else 	
	//滑到底时退出历史界面
	@if "Sprite(1003).value>=History.count"
		@returnto file="system_history" label="*history_exit"
	@endif
	#Sprite(1003).value+=1
//	@jump label="*show_history_bar"
@endif
@return

*history_exit
@deleteevent name="mousewheel"
@action mode="fadeto" target=1000 opacity=0 time=500
@waitaction index=1000
@removeall index=1000 delete=true recursive=true
@event name="exits"  enable=false
@event name="hsbar" enable=false
@event name="rclick" enable=false
@enable_dialogbox
@texton
@restorefont
@return